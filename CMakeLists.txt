#
# Copyright (C) Cybernetica AS
#
# All rights are reserved. Reproduction in whole or part is prohibited
# without the written consent of the copyright owner. The usage of this
# code is subject to the appropriate license agreement.
#

CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(SharemindLibRandom CXX)

INCLUDE("${CMAKE_CURRENT_SOURCE_DIR}/config.local" OPTIONAL)
INCLUDE("${CMAKE_CURRENT_BINARY_DIR}/config.local" OPTIONAL)

SET(SHAREMIND_LIBRANDOM_VERSION "0.1.0")

SET(Boost_USE_STATIC_RUNTIME OFF)
FIND_PACKAGE(LogHard REQUIRED)
FIND_PACKAGE(OpenSSL REQUIRED)
FIND_PACKAGE(SharemindCxxHeaders REQUIRED)

SET(SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES "")
LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES
    ${LogHard_LIBRARIES}
    ${OPENSSL_LIBRARIES}
    ${SharemindCxxHeaders_LIBRARIES}
)
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES)

SET(SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS "")
LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS
    ${LogHard_INCLUDE_DIRS}
    "${OPENSSL_INCLUDE_DIR}"
    ${SharemindCxxHeaders_INCLUDE_DIRS}
)
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS)

SET(SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS "")
LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS
    ${LogHard_DEFINITIONS}
    ${SharemindCxxHeaders_DEFINITIONS}
)
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS)


SET(SHAREMIND_LIBRANDOM_INSTALL_LINK_LIBRARIES
    ${SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES}
    "-L${CMAKE_INSTALL_PREFIX}/lib" "-lsharemind_random")
SET(SHAREMIND_LIBRANDOM_INSTALL_INCLUDE_DIRS
    "${CMAKE_INSTALL_PREFIX}/include"
    ${SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS})
SET(SHAREMIND_LIBRANDOM_INSTALL_DEFINITIONS
    ${SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS})
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_INSTALL_INCLUDE_DIRS)

INCLUDE_DIRECTORIES(${SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS})
ADD_DEFINITIONS(${SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS})
ADD_COMPILE_OPTIONS(
  "-std=c++11" "-Wall" "-Wextra" "-O2" "-fPIC"
  "$<$<NOT:$<STREQUAL:$<CONFIGURATION>,Release>>:-ggdb>"
  "$<$<NOT:$<STREQUAL:$<CONFIGURATION>,Release>>:-fno-omit-frame-pointer>"
  "$<$<STREQUAL:$<CONFIGURATION>,Release>:-DNDEBUG>"
  "$<$<STREQUAL:$<CONFIGURATION>,Release>:-fomit-frame-pointer>"
)

FILE(GLOB_RECURSE SHAREMIND_LIBRANDOM_SOURCES
     "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp")
FILE(GLOB_RECURSE SHAREMIND_LIBRANDOM_HEADERS
     "${CMAKE_CURRENT_SOURCE_DIR}/src/*.h")
ADD_LIBRARY("librandom" SHARED ${SHAREMIND_LIBRANDOM_SOURCES}
                                          ${SHAREMIND_LIBRANDOM_HEADERS})
SET_TARGET_PROPERTIES("librandom" PROPERTIES
                      OUTPUT_NAME "sharemind_random"
                      VERSION "${SHAREMIND_LIBRANDOM_VERSION}"
                      SOVERSION "0.1")
TARGET_LINK_LIBRARIES("librandom"
                      ${SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES})
INSTALL(TARGETS "librandom" LIBRARY DESTINATION "lib")

INSTALL(FILES ${SHAREMIND_LIBRANDOM_HEADERS} DESTINATION "include/sharemind/Random")

FILE(GLOB_RECURSE SHAREMIND_LIBRANDOM_INFILES
     "${CMAKE_CURRENT_SOURCE_DIR}/*.in")
ADD_CUSTOM_TARGET("include_SHAREMIND_LIBRANDOM_INFILES_in_IDE"
                  SOURCES ${SHAREMIND_LIBRANDOM_INFILES})

CONFIGURE_FILE(
  "${CMAKE_CURRENT_SOURCE_DIR}/SharemindLibRandomConfig.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfig.cmake"
  @ONLY)
INSTALL(FILES
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfig.cmake"
  DESTINATION "lib/SharemindLibRandom")
CONFIGURE_FILE(
  "${CMAKE_CURRENT_SOURCE_DIR}/SharemindLibRandomConfigVersion.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfigVersion.cmake"
  @ONLY)
INSTALL(FILES
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfigVersion.cmake"
  DESTINATION "lib/SharemindLibRandom")

