#
# Copyright (C) 2015 Cybernetica
#
# Research/Commercial License Usage
# Licensees holding a valid Research License or Commercial License
# for the Software may use this file according to the written
# agreement between you and Cybernetica.
#
# GNU General Public License Usage
# Alternatively, this file may be used under the terms of the GNU
# General Public License version 3.0 as published by the Free Software
# Foundation and appearing in the file LICENSE.GPL included in the
# packaging of this file.  Please review the following information to
# ensure the GNU General Public License version 3.0 requirements will be
# met: http://www.gnu.org/copyleft/gpl-3.0.html.
#
# For further information, please contact us at sharemind@cyber.ee.
#

CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(SharemindLibRandom CXX)

INCLUDE("${CMAKE_CURRENT_SOURCE_DIR}/config.local" OPTIONAL)
INCLUDE("${CMAKE_CURRENT_BINARY_DIR}/config.local" OPTIONAL)

SET(SHAREMIND_LIBRANDOM_VERSION "0.2.0")

SET(Boost_USE_STATIC_RUNTIME OFF)
FIND_PACKAGE(OpenSSL REQUIRED)
FIND_PACKAGE(SharemindCHeaders REQUIRED)
FIND_PACKAGE(SharemindCxxHeaders REQUIRED)
FIND_PACKAGE(SharemindLibMutexes REQUIRED)

IF(NOT ("${CMAKE_BUILD_TYPE}" STREQUAL "Release"))
    FIND_PATH(VALGRIND_INCLUDE_DIR "valgrind/memcheck.h"
              PATHS "/usr/include/valgrind" "/usr/local/include/valgrind")
    IF(VALGRIND_INCLUDE_DIR)
        MESSAGE(STATUS "Found valgrind/memcheck.h in ${VALGRIND_INCLUDE_DIR}")
    ELSE()
        MESSAGE(STATUS "valgrind/memcheck.h not found!")
    ENDIF()
ENDIF()

SET(SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES "")
LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES
    ${OPENSSL_LIBRARIES}
    ${SharemindCHeaders_LIBRARIES}
    ${SharemindCxxHeaders_LIBRARIES}
    ${SharemindLibMutexes_LIBRARIES}
)
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES)

SET(SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS "")
LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS
    "${OPENSSL_INCLUDE_DIR}"
    ${SharemindCHeaders_INCLUDE_DIRS}
    ${SharemindCxxHeaders_INCLUDE_DIRS}
    ${SharemindLibMutexes_INCLUDE_DIRS}
)
IF(VALGRIND_INCLUDE_DIR)
    LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS
         "${VALGRIND_INCLUDE_DIR}")
ENDIF()
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS)

SET(SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS "")
LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS
    ${SharemindCHeaders_DEFINITIONS}
    ${SharemindCxxHeaders_DEFINITIONS}
)
IF(VALGRIND_INCLUDE_DIR)
    LIST(APPEND SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS
         "-DSHAREMIND_LIBRANDOM_HAVE_VALGRIND")
ENDIF()
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS)


SET(SHAREMIND_LIBRANDOM_INSTALL_LINK_LIBRARIES
    ${SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES}
    "-L${CMAKE_INSTALL_PREFIX}/lib" "-lsharemind_random")
SET(SHAREMIND_LIBRANDOM_INSTALL_INCLUDE_DIRS
    "${CMAKE_INSTALL_PREFIX}/include"
    ${SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS})
SET(SHAREMIND_LIBRANDOM_INSTALL_DEFINITIONS
    ${SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS})
LIST(REMOVE_DUPLICATES SHAREMIND_LIBRANDOM_INSTALL_INCLUDE_DIRS)

INCLUDE_DIRECTORIES(${SHAREMIND_LIBRANDOM_EXTERNAL_INCLUDE_DIRS})
ADD_DEFINITIONS(${SHAREMIND_LIBRANDOM_EXTERNAL_DEFINITIONS})
ADD_COMPILE_OPTIONS(
  "-std=c++11" "-Wall" "-Wextra" "-O2" "-fPIC"
  "$<$<NOT:$<STREQUAL:$<CONFIGURATION>,Release>>:-ggdb>"
  "$<$<NOT:$<STREQUAL:$<CONFIGURATION>,Release>>:-fno-omit-frame-pointer>"
  "$<$<STREQUAL:$<CONFIGURATION>,Release>:-DNDEBUG>"
  "$<$<STREQUAL:$<CONFIGURATION>,Release>:-fomit-frame-pointer>"
)

FILE(GLOB_RECURSE SHAREMIND_LIBRANDOM_SOURCES
     "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp")
FILE(GLOB_RECURSE SHAREMIND_LIBRANDOM_HEADERS
     "${CMAKE_CURRENT_SOURCE_DIR}/src/*.h")
ADD_LIBRARY("librandom" SHARED
    ${SHAREMIND_LIBRANDOM_SOURCES}
    ${SHAREMIND_LIBRANDOM_HEADERS})
SET_TARGET_PROPERTIES("librandom" PROPERTIES
                      OUTPUT_NAME "sharemind_random"
                      VERSION "${SHAREMIND_LIBRANDOM_VERSION}"
                      SOVERSION "0.2")
TARGET_LINK_LIBRARIES("librandom"
                      ${SHAREMIND_LIBRANDOM_EXTERNAL_LINK_LIBRARIES})
INSTALL(TARGETS "librandom" LIBRARY DESTINATION "lib")

SET(SHAREMIND_LIBRANDOM_INSTALL_HEADERS
    "${CMAKE_CURRENT_SOURCE_DIR}/src/CryptographicRandom.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/librandom.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/RandomEngine.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/RandomEngineFactory.h")
INSTALL(FILES ${SHAREMIND_LIBRANDOM_INSTALL_HEADERS}
        DESTINATION "include/sharemind/Random")

FILE(GLOB_RECURSE SHAREMIND_LIBRANDOM_INFILES
     "${CMAKE_CURRENT_SOURCE_DIR}/*.in")
ADD_CUSTOM_TARGET("include_SHAREMIND_LIBRANDOM_INFILES_in_IDE"
                  SOURCES ${SHAREMIND_LIBRANDOM_INFILES})

CONFIGURE_FILE(
  "${CMAKE_CURRENT_SOURCE_DIR}/SharemindLibRandomConfig.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfig.cmake"
  @ONLY)
INSTALL(FILES
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfig.cmake"
  DESTINATION "lib/SharemindLibRandom")
CONFIGURE_FILE(
  "${CMAKE_CURRENT_SOURCE_DIR}/SharemindLibRandomConfigVersion.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfigVersion.cmake"
  @ONLY)
INSTALL(FILES
  "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibRandomConfigVersion.cmake"
  DESTINATION "lib/SharemindLibRandom")

